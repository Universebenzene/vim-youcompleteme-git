# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=baidupcs-go
_pkgname=BaiduPCS-Go
pkgver=3.6
pkgrel=1
pkgdesc="Terminal utility for Baidu Network Disk"
arch=('x86_64')
_importpath="github.com/iikira/${_pkgname}"
url="https://${_importpath}"
license=('Apache')
depends=('glibc')
makedepends=('go-pie' 'git')
conflicts=("baidupcs")
provides=("baidupcs")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('62082f0a1bedfb495d9b096ae06effacbf917cc721ff537522fb95b15e32300927a6cbaccd94f4c2881953af8eb69782d43ac35b195c647b3b67b7c664e3f3d0')

prepare() {
  cd "${srcdir}"

  mkdir -p src/github.com/iikira
  ln -sf "${srcdir}/${_pkgname}-${pkgver}" "src/${_importpath}"
  export GOPATH="${srcdir}:/usr/share/gocode"
}

build() {
  cd "${srcdir}"
  go build -v -work -x "${_importpath}"
}

package() {
  cd "${srcdir}"
  install -Dm755 "${_pkgname}" "${pkgdir}/usr/bin/baidupcs"
  install -Dm644 "${_pkgname}-${pkgver}/README.md" -t "${pkgdir}/usr/share/doc/${pkgname}"
}
